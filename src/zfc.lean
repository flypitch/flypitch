/-
Copyright (c) 2019 The Flypitch Project. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.

Authors: Jesse Han, Floris van Doorn
-/
import .bfol .forcing .forcing_CH

open lattice

open bSet

open fol
local notation h :: t  := dvector.cons h t
local notation `[` l:(foldr `, ` (h t, dvector.cons h t) dvector.nil `]`:0) := l

local infixr ` âŸ¹' `:65 := lattice.imp
local prefix `âˆƒ'` := bd_ex
local prefix `âˆ¼` := bd_not
local infixr ` âŠ“' `:70 := bd_and
local infixr ` âŠ”' `:70 := bd_or

local infix ` âŸ¹'' `:62 := bd_imp

local infix ` â‡”' `:50 := lattice.biimp

-- local infix ` â‡” `:61 := bd_biimp

universe u

section ZFC
inductive ZFC_rel : â„• â†’ Type 1
| Îµ : ZFC_rel 2

inductive ZFC_func : â„• â†’ Type 1
| emptyset : ZFC_func 0
| pr : ZFC_func 2
| Ï‰ : ZFC_func 0
| P : ZFC_func 1
| Union : ZFC_func 1

def L_ZFC : Language.{1} :=
{ functions := ZFC_func,
  relations := ZFC_rel }

end ZFC

section ZFC
variables {Î² : Type 0} [nontrivial_complete_boolean_algebra Î²]

def bSet_model_fun_map : Î  {n : â„•}, L_ZFC.functions n â†’ dvector (bSet Î²) n â†’ bSet Î² :=
begin
  intros n S, induction S,
  from Î» _, bSet.empty,
  from Î» x, by {cases x, refine bSet.pair x_x _, cases x_xs, from x_xs_x},
  from Î» _, bSet.omega,
  from Î» x, by {cases x, exact bv_powerset x_x},
  from Î» x, by {cases x, from bv_union â€¹_â€º}
end

def bSet_model_rel_map : Î  {n : â„•}, L_ZFC.relations n â†’ dvector (bSet Î²) n â†’ Î² :=
begin
  intros n R, induction R,
  intro x, cases x, cases x_xs,
  from x_x âˆˆá´® x_xs_x
end

variable (Î²)
def V : bStructure L_ZFC (Î²) :=
{ carrier := (bSet Î²),
  fun_map := by apply bSet_model_fun_map,
  rel_map := by apply bSet_model_rel_map,
  eq := bv_eq,
  eq_refl := bv_eq_refl,
  eq_symm := by apply bv_eq_symm,
  eq_trans := by apply bv_eq_trans,
  fun_congr :=
  begin
    intros n F, cases F,
      {intros x y, cases x, cases y, simp},
      tactic.rotate 1,
      {intros x y, cases x, cases y, simp},
      {intros x y, cases x, cases y, cases x_xs, cases y_xs,
        change (_ âŠ“ _ : Î²) â‰¤ (bv_powerset _) =á´® (bv_powerset _), simp,
        tidy_context, apply bv_powerset_congr â€¹_â€º },
      {intros x y, cases x, cases y, cases x_xs, cases y_xs,
        change (_ âŠ“ _ : Î²) â‰¤ (bv_union _) =á´® (bv_union _), simp,
        tidy_context, from bv_union_congr â€¹_â€º},
      {intros x y, cases x, cases y, cases x_xs, cases y_xs,
        change (_ âŠ“ (_ âŠ“ _) : Î²) â‰¤ pair x_x x_xs_x =á´® pair y_x y_xs_x,
        cases x_xs_xs, cases y_xs_xs, simp }
  end,
  rel_congr :=
  begin
    intros n R, cases R, intros x y,
    cases x, cases y, cases x_xs, cases y_xs,
    cases x_xs_xs, cases y_xs_xs,
    change ((_ âŠ“ _) âŠ“ (_ âˆˆá´® _) : Î²) â‰¤ (_ âˆˆá´® _), simp,
    tidy_context, apply mem_congr; from â€¹_â€º
  end}

@[simp] lemma carrier_V : â†¥(V Î²) = bSet Î² := rfl

@[simp]lemma V_forall {C : (V Î²) â†’ Î²} : (â¨…(x : V Î²), C x) = (â¨…(x : bSet Î²), C x) := rfl

@[simp]lemma V_exists {C : (V Î²) â†’ Î²} : (â¨†(x : V Î²), C x) = (â¨†(x : bSet Î²), C x) := rfl

@[simp]lemma V_eq {a b} : (V Î²).eq a b = a =á´® b := rfl

@[instance]lemma V_Î²_nonempty : nonempty (V Î²) := âŸ¨bSet.emptyâŸ©

lemma alpha_equivâ‚ {C : (bSet Î²) â†’ Î²} : (â¨…(x : bSet Î²), C x) = â¨…(y : bSet Î²), C y := rfl
lemma alpha_equivâ‚‚ {C : (bSet Î²) â†’ Î²} : (â¨†(x : bSet Î²), C x) = â¨†(y : bSet Î²), C y := rfl

def emptyset {n} : bounded_term L_ZFC n := bd_const ZFC_func.emptyset

notation `âˆ…'` := emptyset

def omega {n} : bounded_term L_ZFC n := bd_const ZFC_func.Ï‰

notation `Ï‰'` := omega

def Powerset {n} : bounded_term L_ZFC n â†’ bounded_term L_ZFC n := bd_app (bd_func ZFC_func.P)

notation `P'` := Powerset

def mem {n} (tâ‚ tâ‚‚ : bounded_term L_ZFC n) : bounded_formula L_ZFC n :=
@bounded_formula_of_relation L_ZFC 2 n ZFC_rel.Îµ tâ‚ tâ‚‚

local infix ` âˆˆ'`:100 := _root_.mem

def pair' {n} (tâ‚ tâ‚‚ : bounded_term L_ZFC n) : bounded_term L_ZFC n :=
@bounded_term_of_function L_ZFC 2 n ZFC_func.pr tâ‚ tâ‚‚

def union' {n} : bounded_term L_ZFC n â†’ bounded_term L_ZFC n := bd_app (bd_func ZFC_func.Union)

notation `â‹ƒ'` := union'

local prefix `&'`:max := bd_var


@[simp] lemma boolean_realize_bounded_formula_mem {n} {v : dvector (V Î²) n}
  (tâ‚ tâ‚‚ : bounded_term L_ZFC n) :
  boolean_realize_bounded_formula v (tâ‚ âˆˆ' tâ‚‚) ([]) =
  boolean_realize_bounded_term v tâ‚ ([]) âˆˆá´® boolean_realize_bounded_term v tâ‚‚ ([]) :=
by refl

@[simp] lemma boolean_realize_bounded_term_Union {n} {v : dvector (V Î²) n}
  (t : bounded_term L_ZFC n) :
  boolean_realize_bounded_term v (â‹ƒ' t) ([]) =
  bv_union (boolean_realize_bounded_term v t ([])) :=
by refl

@[simp] lemma boolean_realize_bounded_term_Powerset {n} {v : dvector (V Î²) n}
  (t : bounded_term L_ZFC n) :
  boolean_realize_bounded_term v (P' t) ([]) =
  bv_powerset (boolean_realize_bounded_term v t ([])) :=
by refl

@[simp] lemma boolean_realize_bounded_term_omega {n} {v : dvector (V Î²) n} :
  boolean_realize_bounded_term v Ï‰' ([]) = bSet.omega :=
by refl

@[simp] lemma boolean_realize_bounded_term_emptyset {n} {v : dvector (V Î²) n} :
  boolean_realize_bounded_term v âˆ…' ([]) = bSet.empty :=
by refl

@[simp]lemma boolean_realize_bounded_term_pair {n} {v : dvector (V Î²) n}
  (tâ‚ tâ‚‚ : bounded_term L_ZFC n) :  boolean_realize_bounded_term v (pair' tâ‚ tâ‚‚) ([]) =
  pair (boolean_realize_bounded_term v tâ‚ ([])) (boolean_realize_bounded_term v tâ‚‚ ([])) :=
by refl

@[simp] lemma fin_0 {n : â„•} : (0 : fin (n+1)).1 = 0 := by refl
@[simp] lemma fin_1 {n : â„•} : (1 : fin (n+2)).1 = 1 := by refl
@[simp] lemma fin_2 {n : â„•} : (2 : fin (n+3)).1 = 2 := by refl
@[simp] lemma fin_3 {n : â„•} : (3 : fin (n+4)).1 = 3 := by refl

def axiom_of_emptyset : sentence L_ZFC := âˆ€' (âˆ¼(&0 âˆˆ' âˆ…'))

lemma bSet_models_emptyset : âŠ¤ âŠ©[V Î²] axiom_of_emptyset :=
by {change âŠ¤ â‰¤ _, simp[axiom_of_emptyset, -top_le_iff], intro x, from empty_spec}

-- axiom of ordered pairs
-- âˆ€x y z w, (x, y) = (z, w) â†” x = z âˆ§ y = w
def axiom_of_ordered_pairs : sentence L_ZFC :=
 âˆ€' âˆ€' âˆ€' âˆ€'(((pair' &'3 &'2 â‰ƒ pair' &'1 &'0)) â‡” (&'3 â‰ƒ &'1 âŠ“ &'2 â‰ƒ &'0))

lemma bSet_models_ordered_pairs : âŠ¤ âŠ©[V Î²] axiom_of_ordered_pairs :=
begin
  change âŠ¤ â‰¤ _, simp[axiom_of_ordered_pairs], intros a b x y, tidy,
  from eq_of_eq_pair_left, from eq_of_eq_pair_right
end

-- axiom of extensionality
-- âˆ€ x y, (âˆ€ z, (z âˆˆ x â†” z âˆˆ y)) â†’ x = y
def axiom_of_extensionality : sentence L_ZFC :=
âˆ€' âˆ€' (âˆ€'(&'0  âˆˆ' &'2 â‡”  &'0 âˆˆ' &'1) âŸ¹ (&1 â‰ƒ &0))

lemma bSet_models_extensionality : âŠ¤ âŠ©[V Î²] axiom_of_extensionality :=
by { simp [forced_in, axiom_of_extensionality], exact bSet_axiom_of_extensionality }

-- axiom schema of "strong" collection
-- For every formula `Ï•(x,y,p)` with (at most) `n+2` free variables (`p` is a vector of length `n`),
-- âˆ€ p âˆ€ A, (âˆ€ x âˆˆ A, âˆƒ y, Ï•(x,y,p)) âŸ¹
--  (âˆƒ B, (âˆ€ x âˆˆ A, âˆƒ y âˆˆ B, Ï•(x,y,p)) âˆ§ âˆ€ y âˆˆ B, âˆƒ x âˆˆ A, Ï•(x,y,p))
def axiom_of_collection {n} (Ï• : bounded_formula L_ZFC (n+2)) : sentence L_ZFC :=
bd_alls (n+1) $ (âˆ€' (&'0 âˆˆ' &'1 âŸ¹ âˆƒ' (Ï• â†‘' 1 # 2))) âŸ¹
(âˆƒ' (âˆ€'(&'0 âˆˆ' &'2 âŸ¹ âˆƒ' (&'0 âˆˆ' &'2 âŠ“ (Ï• â†‘' 2 # 2))) âŠ“
     âˆ€'(&'0 âˆˆ' &'1 âŸ¹ âˆƒ' (&'0 âˆˆ' &'3 âŠ“' ((Ï• â†‘' 3 # 2)[&'1/0] : _)))))

lemma lift2_helper {L n l} (f : bounded_preformula L n l) {k} (m : â„•) :
  f â†‘' (k+2) # m = ((f â†‘' (k+1) # m) â†‘' 1 # m : _) :=
by { ext, simp only [lift_bounded_formula_fst], rw [lift_formula_at2_medium], refl, linarith }

lemma B_ext_left_realize_bounded_formula {n : â„•} (Ï• : bounded_formula L_ZFC (n + 1)) (xs : dvector (V Î²) n) : âˆ€ (x y : V Î²), x =á´® y âŠ“ (boolean_realize_bounded_formula (x::xs) Ï• dvector.nil) â‰¤ boolean_realize_bounded_formula (y::xs) Ï• dvector.nil :=
begin
  intros x y,
  suffices : (x =á´® y = â¨… (m : fin (n+1)), (V Î²).eq ((x::xs).nth _ m.is_lt) ((y::xs).nth _ m.is_lt)),
    by {rw this, apply boolean_realize_bounded_formula_congr, apply_instance},
  refine le_antisymm _ _,
    { apply le_infi, rintro âŸ¨m,HmâŸ©,
      cases m,
        { refl },
        { rw [dvector.nth_cons, dvector.nth_cons],
          {exact bSet.bv_refl, { exact nat.lt_of_succ_lt_succ Hm }},
      }},
    { tidy_context, exact a âŸ¨0, dec_trivialâŸ© }
end

lemma B_ext_right_realize_bounded_formula {n : â„•} (Ï• : bounded_formula L_ZFC (n + 2)) (xs : dvector (V Î²) n) : âˆ€ (x y z : V Î²), x =á´® y âŠ“ (boolean_realize_bounded_formula (z::x::xs) Ï• dvector.nil) â‰¤ boolean_realize_bounded_formula (z::y::xs) Ï• dvector.nil :=
begin
  intros x y z,
  suffices : (x =á´® y = â¨… (m : fin (n+2)), (V Î²).eq ((z::x::xs).nth _ m.is_lt) ((z::y::xs).nth _ m.is_lt)),
    by {rw this, apply boolean_realize_bounded_formula_congr, apply_instance},
  refine le_antisymm _ _,
    { apply le_infi, rintro âŸ¨m,HmâŸ©,
      cases m,
        { exact bSet.bv_refl },
        { cases m,
          { refl },
          { repeat {rw dvector.nth_cons},
            { exact bSet.bv_refl, apply nat.lt_of_succ_lt_succ,
              apply nat.lt_of_succ_lt_succ, from â€¹_â€º }} }},
    { tidy_context, exact a âŸ¨1, dec_trivialâŸ© }
end

lemma bSet_models_collection {n} (Ï• : bounded_formula L_ZFC (n+2)) : âŠ¤ âŠ©[V Î²] axiom_of_collection Ï• :=
begin
  change âŠ¤ â‰¤ _, simp only [axiom_of_collection, boolean_realize_sentence_bd_alls],
  bv_intro xs, cases xs with _ u xs,
  simp only
    [ boolean_realize_bounded_formula_and,
      boolean_realize_bounded_term, imp_top_iff_le,
      boolean_realize_bounded_formula_ex, top_le_iff,
      boolean_realize_bounded_formula, boolean_realize_formula_insert_lift2,
      lift2_helper, boolean_realize_subst_formula0, fin_2 ],
  have := bSet_axiom_of_collection
            (Î» a b : V Î², boolean_realize_bounded_formula (b :: a :: xs) Ï• ([])) _ _ u,
  simp only [lattice.top_le_iff, bSet.mem, lattice.imp_top_iff_le, lattice.le_infi_iff] at this,
  exact this,
  { intros, apply B_ext_left_realize_bounded_formula },
  { intros, apply B_ext_right_realize_bounded_formula },
end

-- axiom of union
-- âˆ€ u x, x âˆˆ â‹ƒ u â†” âˆƒ y âˆˆ u, x âˆˆ y
def axiom_of_union : sentence L_ZFC :=
âˆ€' âˆ€' (&'0 âˆˆ' â‹ƒ' &'1 â‡” (âˆƒ' (&'0 âˆˆ' &'2 âŠ“ &'1 âˆˆ' &'0)))

lemma bSet_models_union : âŠ¤ âŠ©[V Î²] axiom_of_union :=
begin
  simp [-top_le_iff, forced_in, axiom_of_union, -lattice.le_inf_iff],
  intros x z,
  have := @bv_union_spec' _ _ x âŠ¤,
  replace this := this z, dsimp at this,
  bv_split, bv_split_goal
end

-- axiom of powerset
-- âˆ€ u x, x âˆˆ P(u) â†” âˆ€ y âˆˆ x, y âˆˆ u

def axiom_of_powerset : sentence L_ZFC :=
  âˆ€' âˆ€' (&'0 âˆˆ' P' &'1 â‡” (âˆ€' (&'0 âˆˆ' &'1 âŸ¹ &'0 âˆˆ' &'2)))

lemma bSet_models_powerset : âŠ¤ âŠ©[V Î²] axiom_of_powerset :=
begin
  simp [forced_in, axiom_of_powerset, -lattice.le_inf_iff, -top_le_iff],
  intros x z, have := @bv_powerset_spec _ _ x z,
  rw [subset_unfold'] at this,
  apply le_inf, bv_imp_intro, exact this.mpr H, bv_imp_intro, exact this.mp H
end

/-- &1 âŠ† &0 â†” âˆ€ z, (z âˆˆ &1 âŸ¹ z âˆˆ &0)-/
def subset'' {n} (tâ‚ tâ‚‚ : bounded_term L_ZFC n): bounded_formula L_ZFC n :=
âˆ€' (&'0 âˆˆ' (tâ‚ â†‘ 1) âŸ¹ &'0 âˆˆ' (tâ‚‚ â†‘ 1))

local infix ` âŠ†'`:100 := subset''

@[simp] lemma boolean_realize_bounded_formula_subset {n} {v : dvector (V Î²) n}
  (tâ‚ tâ‚‚ : bounded_term L_ZFC n) :
  boolean_realize_bounded_formula v (tâ‚ âŠ†' tâ‚‚) ([]) =
  boolean_realize_bounded_term v tâ‚ ([]) âŠ†á´® boolean_realize_bounded_term v tâ‚‚ ([]) :=
by { simp [subset'', subset_unfold'] }

/- `z` is transitive if `âˆ€ x, x âˆˆ z âŸ¹ x âŠ† z` -/
def is_transitive_f : bounded_formula L_ZFC 1 := âˆ€' ((&'0 âˆˆ' &'1) âŸ¹ &'0 âŠ†' &'1)

/- `z` is `âˆˆ`-trichotomous if `âˆ€ x y âˆˆ z, x = y âˆ¨ x âˆˆ y âˆ¨ y âˆˆ x` -/
def epsilon_trichotomy_f : bounded_formula L_ZFC 1 :=
âˆ€' ((&'0 âˆˆ' &'1) âŸ¹''(âˆ€' (&'0 âˆˆ' &'2 âŸ¹'' (&'1 â‰ƒ &'0 âŠ”' &'1 âˆˆ' &'0) âŠ”' &'0 âˆˆ' &'1)))

/- `z` is `âˆˆ`-well-founded if `âˆ€ x, x âŠ† z âŸ¹ x â‰  âˆ… âŸ¹ âˆƒy âˆˆ x. âˆ€w âˆˆ x. w âˆ‰ y`.
  Note: this is true for every set by regularity, so we don't have to assume this. But we do it for
  completeness, to explicitly state that the order relation is well-founded. -/
def epsilon_well_founded_f : bounded_formula L_ZFC 1 :=
âˆ€' (((&'0 âŠ†' &'1) âŸ¹'' ((âˆ¼(&'0 â‰ƒ âˆ…')) âŸ¹'' âˆƒ' (&'0 âˆˆ' &'1 âŠ“' (âˆ€' (&'0 âˆˆ' &'2 âŸ¹'' âˆ¼(&'0 âˆˆ' &'1)))))))

/- `z` is `âˆˆ`-well-order if it is `âˆˆ`-well-founded and `âˆˆ`-trichotomous. -/
def ewo_f : bounded_formula L_ZFC 1 := epsilon_trichotomy_f âŠ“' epsilon_well_founded_f

/- `z` is an ordinal if forms is an `âˆˆ`-well-order and `âˆˆ` is transitive on `z`. -/
def Ord_f : bounded_formula L_ZFC 1 := ewo_f âŠ“' is_transitive_f

@[simp]lemma Ord_f_is_Ord {x : V Î²} : boolean_realize_bounded_formula (by exact [x]) Ord_f dvector.nil = Ord x :=
by {simp [Ord_f,ewo_f,is_transitive_f,epsilon_well_founded_f, epsilon_trichotomy_f], refl}

-- this is the usual axiom of infinity, plus a characterization of omega as the least limit ordinal
def axiom_of_infinity : sentence L_ZFC :=
  (âˆ…' âˆˆ' Ï‰' âŠ“' âˆ€'(&'0 âˆˆ' Ï‰' âŸ¹ âˆƒ' (&'0 âˆˆ' Ï‰' âŠ“' &'1 âˆˆ' &'0)))
  âŠ“' (âˆƒ' (Ord_f âŠ“' Ï‰' â‰ƒ &'0))
  âŠ“' âˆ€' (Ord_f âŸ¹ ((âˆ…' âˆˆ' &'0 âŠ“' âˆ€'(&'0 âˆˆ' &'1 âŸ¹ âˆƒ' (&'0 âˆˆ' &'2 âŠ“' &'1 âˆˆ' &'0))) âŸ¹ Ï‰' âŠ†' &0))

lemma bSet_models_infinity : âŠ¤ âŠ©[V Î²] axiom_of_infinity :=
begin
  simp [forced_in, axiom_of_infinity, boolean_realize_sentence,
    -lattice.le_inf_iff, -top_le_iff],
  refine le_inf _ _,
    { exact bSet_axiom_of_infinity' },
    { refine le_inf _ _,
      { apply bv_use bSet.omega, exact le_inf Ord_omega bv_refl },
      { exact omega_least_is_limit } }
end

-- axiom of regularity
-- âˆ€ x, x â‰  âˆ… âŸ¹ âˆƒ y âˆˆ x, âˆ€ z âˆˆ x, z âˆ‰ y

def axiom_of_regularity : sentence L_ZFC :=
  âˆ€' (âˆ¼(&0 â‰ƒ âˆ…') âŸ¹ (âˆƒ' (&'0 âˆˆ' &'1 âŠ“ âˆ€' (&'0 âˆˆ' &'2 âŸ¹ âˆ¼(&'0 âˆˆ' &'1)))))

lemma bSet_models_regularity : âŠ¤ âŠ©[V Î²] axiom_of_regularity :=
begin
  change âŠ¤ â‰¤ _, unfold axiom_of_regularity,
  simp[-top_le_iff], intro x,
  bv_imp_intro,
  apply bSet_axiom_of_regularity, convert H
end

/- âˆ€ x, x â‰  âˆ… âˆ§ ((âˆ€ y, y âŠ† x âˆ§ âˆ€ wâ‚ wâ‚‚ âˆˆ y, wâ‚ âŠ† wâ‚‚ âˆ¨ wâ‚‚ âŠ† wâ‚) â†’ (â‹ƒy) âˆˆ x)
      â†’ âˆƒ c âˆˆ x, âˆ€ z âˆˆ x, c âŠ† z â†’ c = z -/
def zorns_lemma : sentence L_ZFC :=
âˆ€' (âˆ¼ (&'0 â‰ƒ âˆ…')
  âŸ¹ (âˆ€' (&'0 âŠ†' &'1 âŠ“' (âˆ€' âˆ€' ((&'1 âˆˆ' &'2 âŠ“' &'0 âˆˆ' &'2) âŸ¹ (&'1 âŠ†' &'0 âŠ”' &'0 âŠ†' &'1)))
    âŸ¹ (â‹ƒ' &' 0 âˆˆ' &'1)))
    âŸ¹  (âˆƒ' (&'0 âˆˆ' &'1 âŠ“ âˆ€' (&'0 âˆˆ' &'2 âŸ¹ &'1 âŠ†' &'0 âŸ¹ &'1 â‰ƒ &'0 ))))

lemma bSet_models_Zorn : âŠ¤ âŠ©[V Î²] zorns_lemma :=
begin
  simp [forced_in, zorns_lemma, boolean_realize_sentence, -lattice.le_inf_iff, -top_le_iff, -lattice.le_infi_iff],
  from bSet_zorns_lemma'
end

def ZFC : Theory L_ZFC :=
  {axiom_of_emptyset, axiom_of_ordered_pairs, axiom_of_extensionality, axiom_of_union,
   axiom_of_powerset, axiom_of_infinity, axiom_of_regularity, zorns_lemma} âˆª
  set.Union (Î»(n : â„•), axiom_of_collection '' (set.univ : set $ bounded_formula L_ZFC (n+2)))

theorem bSet_models_ZFC : âŠ¤ âŠ©[V Î²] ZFC :=
begin
  change âŠ¤ â‰¤ _, bv_intro f, bv_intro H,
  repeat{auto_cases}; try{subst H}; try {cases H},
  from bSet_models_Zorn _,
  from bSet_models_regularity _,
  from bSet_models_infinity _,
  from bSet_models_powerset _,
  from bSet_models_union _,
  from bSet_models_extensionality _,
  from bSet_models_ordered_pairs _,
  from bSet_models_emptyset _,
  from bSet_models_collection _ â€¹_â€º
end

include Î²
theorem ZFC_consistent : is_consistent ZFC := consis_of_exists_bmodel (bSet_models_ZFC Î²)
omit Î²

/-- f is =á´®-extensional if for every wâ‚ wâ‚‚ vâ‚ vâ‚‚, if pair (wâ‚, vâ‚) and pair (wâ‚‚, vâ‚‚) âˆˆ f and
    wâ‚ =á´® wâ‚‚, then vâ‚ =á´® vâ‚‚ -/
def is_func_f : bounded_formula L_ZFC 1 :=
âˆ€' âˆ€' âˆ€' âˆ€' ((pair' &'3 &'1 âˆˆ' &'4 âŠ“' pair' &'2 &'0 âˆˆ' &'4
  âŸ¹ (&'3 â‰ƒ &'2 âŸ¹ &'1 â‰ƒ &'0)))

@[simp]lemma realize_is_func_f {f : V Î²} : boolean_realize_bounded_formula (by exact [f]) is_func_f dvector.nil = is_func f :=
begin
  simp[is_func_f, bSet.is_func], refl
end

def is_total'_f : bounded_formula L_ZFC 3 :=
(âˆ€' (&'0 âˆˆ' &'3 âŸ¹ (âˆƒ' (&'0 âˆˆ' &'3 âŠ“' (pair' &'1 &'0 âˆˆ' &'2)))))

@[simp]lemma realize_is_total'_f {x y f : V Î²} : boolean_realize_bounded_formula (by exact [f, y, x]) is_total'_f dvector.nil = is_total x y f :=
begin
  simp [bSet.is_total, is_total'_f]
end

-- is_total'_fâ‚‚ S y f is the same as is_total'_f y S f
def is_total'_fâ‚‚ : bounded_formula L_ZFC 3 :=
(âˆ€' (&'0 âˆˆ' &'2 âŸ¹ (âˆƒ' (&'0 âˆˆ' &'4 âŠ“' (pair' &'1 &'0 âˆˆ' &'2)))))

@[simp]lemma realize_is_total'_fâ‚‚ {x y f : V Î²} : boolean_realize_bounded_formula (by exact [f, y, x]) is_total'_fâ‚‚ dvector.nil = is_total y x f :=
begin
  rw [bSet.is_total, is_total'_fâ‚‚], simp, refl
end

def is_func'_f : bounded_formula L_ZFC 3 :=
  (is_func_f.cast (dec_trivial)) âŠ“' is_total'_f

def is_func'_fâ‚‚ : bounded_formula L_ZFC 3 :=
(is_func_f.cast dec_trivial) âŠ“' is_total'_fâ‚‚

@[simp]lemma realize_is_func'_f {x y f : V Î²} : boolean_realize_bounded_formula (by exact [f, y, x]) is_func'_f dvector.nil = is_func' x y f :=
by simp [is_func'_f, is_func']

@[simp]lemma realize_is_func'_fâ‚‚ {x y f : V Î²} : boolean_realize_bounded_formula (by exact [f, y, x]) is_func'_fâ‚‚ dvector.nil = is_func' y x f :=
by simp [is_func'_fâ‚‚, is_func']

/-
  `at_most_f x y` means
  `âˆƒ S, âˆƒ f, S âŠ† y âˆ§ f contains a function from S to x âˆ§ f surjects S onto x`
  In `bSet` it corresponds to the formula `larger_than y x`.

  `at_most_f x y` is equivalent to `Â¬ y â‰º x`.
-/
def at_most_f : bounded_formula L_ZFC 2 :=
âˆƒ' (âˆƒ' (((&'1 âŠ†' &'3) âŠ“' (is_func'_fâ‚‚).cast (dec_trivial : 3 â‰¤ 4)) âŠ“'
        âˆ€' ( &0 âˆˆ' &3 âŸ¹ (âˆƒ' (&'0 âˆˆ' &'3 âŠ“' pair' &'0 &'1 âˆˆ' &'2)))))

@[simp]lemma realize_at_most_f {x y : V Î²} :
  boolean_realize_bounded_formula ([y,x]) at_most_f dvector.nil = larger_than x y :=
by simp[larger_than, at_most_f, is_func]


def is_inj_f : bounded_formula L_ZFC 1 :=
âˆ€' âˆ€' âˆ€' âˆ€' (((pair' &'3 &'1 âˆˆ' &'4 âŠ“' pair' &'2 &'0 âˆˆ' &'4) âŠ“ &'1 â‰ƒ &'0) âŸ¹ &'3 â‰ƒ &'2)

@[simp]lemma realize_is_inj_f (f : V Î²) :
  boolean_realize_bounded_formula (by exact [f]) is_inj_f dvector.nil = is_inj f :=
by {simp[is_inj_f, is_inj], refl}

def injects_into_f : bounded_formula L_ZFC 2 :=
 âˆƒ' (is_func'_f âŠ“' is_inj_f.cast (dec_trivial))

@[simp]lemma realize_injects_into {x y : V Î²} :
  boolean_realize_bounded_formula (by exact [y,x]) injects_into_f dvector.nil = injects_into x y :=
by {simp[injects_into_f, injects_into]}

def non_empty_f : bounded_formula L_ZFC 1 := âˆ¼(&'0 â‰ƒ âˆ…')

@[simp]lemma non_empty_f_is_non_empty {x : V Î²} : boolean_realize_bounded_formula (by exact [x]) non_empty_f dvector.nil = not_empty x := by {simp[non_empty_f], refl}

/-- The continuum hypothesis is given by the formula
  `âˆ€x, x is an ordinal âŸ¹ x â‰¤ Ï‰ âˆ¨ P(Ï‰) â‰¤ x`.
  Here `a â‰¤ b` means there is a surjection from a subset of `b` to `a`.
  We have to perform two substitutions (`substmax_bounded_formula` and `[../0]`)
  to apply `at_most_f` to the appropriate arguments. -/
def CH_f : sentence L_ZFC :=
âˆ€' (Ord_f âŸ¹ (substmax_bounded_formula at_most_f Ï‰' âŠ”' at_most_f[Powerset omega/0]))

variable {Î²}
lemma CH_f_is_CH : âŸ¦CH_fâŸ§[V Î²] = CHâ‚‚ :=
begin
  have h1 : âˆ€(x : V Î²), boolean_realize_bounded_formula ([x])
    (substmax_bounded_formula at_most_f omega) ([]) =
    boolean_realize_bounded_formula ([x,omega]) at_most_f ([]),
  { intro, refl },
  have h2 : âˆ€(x : V Î²), boolean_realize_bounded_formula ([x]) (at_most_f[P' omega /0]) ([]) =
    boolean_realize_bounded_formula (([bv_powerset omega, x] : dvector (V Î²) 2)) at_most_f ([]),
  { intro, refl },
  -- note: once we have proven realize_substmax_bf and realize_subst0_bf, we can add them to this simp set
  simp [-substmax_bounded_formula, CH_f, CHâ‚‚, neg_supr, sup_assoc, h1, h2, lattice.imp]
end

lemma CH_f_sound {Î“ : Î²} : Î“ âŠ©[V Î²] CH_f â†” Î“ â‰¤ CHâ‚‚ :=
by {change _ â‰¤ _ â†” _ â‰¤ _, rw CH_f_is_CH}

lemma neg_CH_f_sound {Î“ : Î²} : Î“ âŠ©[V Î²] âˆ¼CH_f â†” Î“ â‰¤ - CHâ‚‚ :=
by {change _ â‰¤ _ â†” _ â‰¤ _, rw [boolean_realize_sentence_not, CH_f_is_CH]}

end ZFC

open pSet cardinal

section CH_unprovable


lemma V_ğ”¹_cohen_models_neg_CH : âŠ¤ âŠ©[V ğ”¹_cohen] âˆ¼CH_f :=
begin
  rw neg_CH_f_sound, exact neg_CHâ‚‚
end

instance V_ğ”¹_nonempty : nonempty (V ğ”¹_cohen) := âŸ¨bSet.emptyâŸ©

theorem CH_f_unprovable : Â¬ (ZFC âŠ¢' CH_f) :=
unprovable_of_model_neg _ (bSet_models_ZFC _) (nontrivial.bot_lt_top) V_ğ”¹_cohen_models_neg_CH

end CH_unprovable

open collapse_algebra

section neg_CH_unprovable

instance V_ğ”¹_collapse_nonempty : nonempty (V ğ”¹_collapse) := âŸ¨bSet.emptyâŸ©

lemma V_ğ”¹_collapse_models_CH : âŠ¤ âŠ©[V ğ”¹_collapse] CH_f :=
by { rw CH_f_sound, exact CHâ‚‚_true }

theorem neg_CH_f_unprovable : Â¬ (ZFC âŠ¢' âˆ¼CH_f) :=
unprovable_of_model_neg (V ğ”¹_collapse) (bSet_models_ZFC _)
  (nontrivial.bot_lt_top) (by {rw forced_in_not, from V_ğ”¹_collapse_models_CH})

end neg_CH_unprovable

section
-- a nicer formulation of CH using formulae

@[simp] def Powerset_t : term L_ZFC â†’ term L_ZFC := app (func ZFC_func.P)
@[simp] def omega_t : term L_ZFC := func ZFC_func.Ï‰
@[simp] def leq_f : formula L_ZFC := at_most_f.fst
@[simp] def is_ordinal : formula L_ZFC := Ord_f.fst

def CH_formula : formula L_ZFC :=
âˆ€' (is_ordinal âŸ¹ leq_f[omega_t//1] âŠ” leq_f[Powerset_t omega_t//0])

lemma CH_f_fst : CH_f.fst = CH_formula :=
by { simp [CH_f, CH_formula, -substmax_bounded_formula], refl }

end
